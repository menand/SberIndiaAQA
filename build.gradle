plugins {
    id 'java-library'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'io.qameta.allure' version '2.12.0'
}

def allureVersion = "2.29.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencyUpdates {
    resolutionStrategy {
        componentSelection {
            all { ComponentSelection selection ->
                def unstableKeywords = ['alpha', 'beta', 'rc', 'm', 'snapshot']
                boolean isUnstable = unstableKeywords.any { keyword ->
                    selection.candidate.version.toLowerCase().contains(keyword)
                }
                if (isUnstable) {
                    selection.reject("Unstable version: ${selection.candidate.version}")
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            'io.qameta.allure:allure-selenide:2.29.1',
            'org.junit.jupiter:junit-jupiter-api:5.12.2',
            'org.junit.jupiter:junit-jupiter-params:5.12.2',
            'org.junit.platform:junit-platform-launcher:1.12.2',
            'org.slf4j:slf4j-simple:2.0.17',
            'org.apache.logging.log4j:log4j-to-slf4j:2.24.3',
            'com.codeborne:selenide:7.9.1')
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.12.2')
}


tasks.withType(Test).configureEach {
    systemProperties(System.getProperties())
    useJUnitPlatform()
    systemProperty "junit.jupiter.execution.parallel.enabled", "true"
    systemProperty "junit.jupiter.executions.parallel.config.strategy", "fixed"
    systemProperty "junit.jupiter.executions.parallel.config.fixed.parallelism", "2"

    systemProperty "selenide.reportsFolder", "build/reports/${name}"
    systemProperty "selenide.downloadsFolder", "build/downloads/${name}"

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register('all_tests', Test) {
    useJUnitPlatform {

    }
}

tasks.register('smoke', Test) {
    useJUnitPlatform {
        includeTags("smoke")
    }
}